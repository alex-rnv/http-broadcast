plugins {
    id 'groovy'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.3.1"
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

group 'com.alexrnv'
version '1.0.0'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.shadow
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

shadowJar {
    classifier = ''
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Starter'
        attributes 'Main-Verticle': 'com.alexrnv.httpbroadcast.upstream.HttpDistributionServer'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['shadow']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'http-distribution-server'
        vcsUrl = 'https://github.com/alex-rnv/http-broadcast'
        licenses = ['Apache-2.0']
        labels = ['httpds']
        publicDownloadNumbers = true
        version {
            name = project.version //Bintray logical version name
        }
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.11'
    compile 'io.vertx:vertx-core:3.0.0'
    compile 'io.vertx:vertx-lang-groovy:3.0.0'
    compile 'ch.qos.logback:logback-classic:1.1.+'
    compile 'org.slf4j:slf4j-api:1.7.12'

    testCompile 'io.vertx:vertx-unit:3.0.0'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}
